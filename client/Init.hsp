//--------------------------------------------------------------------------------------------------
//= init.hsp
//--------------------------------------------------------------------------------------------------

	//----------------------------------------------------------------------------------------------
	//= コンパイルオプション
	//----------------------------------------------------------------------------------------------
		#packopt hide 1
		randomize

	//----------------------------------------------------------------------------------------------
	//= wininetを初期化
	//----------------------------------------------------------------------------------------------
		netinit
		if( stat != 0 ){
			dialog "ネットワーク接続モジュールの初期化に失敗しました。"
			end
		}

	//----------------------------------------------------------------------------------------------
	//= 初期設定がされていなければ終了し設定させる
	//----------------------------------------------------------------------------------------------
		exist "data.dat"
		if( strsize == -1 ){
			dialog "初期設定を行なってください。", 0, ""+PROGRAM_NAME
			sdim ln,4096	: sdim buf,32000
			pipeexec buf, "setting.exe", 1
			if stat : dialog "初期設定に失敗しました。\n[setting.exe]が見つかりませんでした。",1,"エラー" : goto *__end
			repeat
				pipeget ln
				if( stat == 0 ){
					break
				}
				wait 10
			loop
			exist "data.dat"
			if( strsize == -1){
				dialog "初期設定が行われなかったため終了します。",1,"エラー"
				goto *__end
			}
		}

	//----------------------------------------------------------------------------------------------
	//= 設定ファイルをロード
	//----------------------------------------------------------------------------------------------
		gosub *__DataLoad

	//----------------------------------------------------------------------------------------------
	//= メインウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		//非表示で初期化
		bgscr SCR_MAIN, ginfo_dispx, ginfo_dispy, 2, 0, 0, ginfo_dispx, ginfo_dispx
		//ウィンドウを透過+タスクバーに表示しないようにする
		GetWindowLongA hwnd, GWL_EXSTYLE
		SetWindowLongA hwnd, GWL_EXSTYLE, stat | 0x00080000+WS_EX_TOOLWINDOW
		SetLayered hwnd, , 1, 2

	//----------------------------------------------------------------------------------------------
	//= 範囲選択ウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		//画面外に初期化
		bgscr SCR_SELECT, ginfo_dispx, ginfo_dispy, 2, ginfo_dispx, ginfo_dispy, 1, 1
		//ウィンドウを透過+タスクバーに表示しないようにする
		GetWindowLongA hwnd, GWL_EXSTYLE
		SetWindowLongA hwnd, GWL_EXSTYLE, stat | 0x00080000+WS_EX_TOOLWINDOW
		SetLayered hwnd, , 128, 2
		//ウィンドウを表示
		gsel SCR_SELECT, 2

	//----------------------------------------------------------------------------------------------
	//= 範囲選択中操作方法ウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		//画面外に初期化
		bgscr SCR_SELECTHELP, SELECTHELP_SIZE_W, SELECTHELP_SIZE_H, 2, ginfo_dispx, ginfo_dispy
		//ウィンドウを透過+タスクバーに表示しないようにする
		GetWindowLongA hwnd, GWL_EXSTYLE
		SetWindowLongA hwnd, GWL_EXSTYLE, stat | 0x00080000+WS_EX_TOOLWINDOW
		SetLayered hwnd, , 128, 2
		//ウィンドウを表示
		gsel SCR_SELECTHELP, 2
		gosub *__SelectHelpDraw

	//----------------------------------------------------------------------------------------------
	//= 編集ウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		//非表示で初期化
		screen SCR_EDIT, EDITWINDOW_SIZE_W, EDITWINDOW_SIZE_H, 2+4
		width , , ginfo_dispx/2-ginfo_sizex/2, ginfo_dispy/2-ginfo_sizey/2
		title ""+PROGRAM_NAME+" "+PROGRAM_VERSION
		//最小化/最大化ボタンを無効化
		StyleAdd hwnd, $20000, 1+0
		//アップロード/保存/キャンセルボタン設置
		m = "アップロード", "ローカルへ保存", "キャンセル"
		l = *__Upload, *__LocalSave, *__Cancel
		n = length(l)
		margin = 2
		x = CTRLBAR_POS_X+margin			: y = CTRLBAR_POS_Y+margin
		w = (CTRLBAR_SIZE_W-margin*2)/n		: h = CTRLBAR_SIZE_H-margin*2
		objsize w, h
		repeat n
			pos x+(w+margin)*cnt, y
			button gosub ""+m(cnt), l(cnt)
		loop

	//----------------------------------------------------------------------------------------------
	//= ボタンアイコンバッファ初期化
	//----------------------------------------------------------------------------------------------
		celload "data/image/ButtonIcon.png", SCR_BUTTONICON
		celdiv SCR_BUTTONICON, BUTTON_ICON_SIZE_WH, BUTTON_ICON_SIZE_WH, BUTTON_ICON_SIZE_WH/2, BUTTON_ICON_SIZE_WH/2

	//----------------------------------------------------------------------------------------------
	//= マウスカーソルを十字型に変更
	//----------------------------------------------------------------------------------------------
		SetSystemCursor IDC_CROSS