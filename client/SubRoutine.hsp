//--------------------------------------------------------------------------------------------------
//= SubRoutine.hsp
//--------------------------------------------------------------------------------------------------
//	サブルーチン
//--------------------------------------------------------------------------------------------------

	#include "module/SaveLoad.hsp"

	//----------------------------------------------------------------------------------------------
	//= 終了処理
	//----------------------------------------------------------------------------------------------
		*__end
			end

	//----------------------------------------------------------------------------------------------
	//= 範囲選択ループから編集ループへ移る際の引継ぎ処理
	//----------------------------------------------------------------------------------------------
		*__SelectToEdit
			//マウスカーソルを元に戻す
			SetSystemCursor IDC_ARROW
			//キャプチャサイズが0x0にならないために修正
			if( CaptureSizeW < 1 ){ CaptureSizeW = 1 }
			if( CaptureSizeH < 1 ){ CaptureSizeH = 1 }
			//スクリーンショット撮影
			buffer SCR_SSBUF, CaptureSizeW, CaptureSizeH
			GetScreenShot CapturePosX, CapturePosY, CaptureSizeW, CaptureSizeH, 0
			//ペンバッファ初期化
			buffer SCR_PENBUF, CaptureSizeW, CaptureSizeH
			COLSET_ALPHA:boxf
			//アンドゥバッファ初期化
			buffer SCR_ANDOBUF, CaptureSizeW, CaptureSizeH
			COLSET_ALPHA:boxf
			//編集ウィンドウを表示
			gsel SCR_EDIT, 1
			//編集画面用変数を初期化
			EditPenSize = 5
			EditColor = 0
			if( CaptureSizeW < EDITWINDOW_SIZE_W ){
				EditPosX = ginfo_winx/2-CaptureSizeW/2
			} else {
				EditPosX = 64
			}
			if( CaptureSizeH < EDITWINDOW_SIZE_H ){
				EditPosY = ginfo_winy/2-CaptureSizeH/2
			} else {
				EditPosY = 64
			}
			//パレットボタン設置
			syscolor 4	: bg = ginfo_r, ginfo_g, ginfo_b
			margin = 2
			w = PALETTEBAR_SIZE_H-margin*2	: h = w
			x = PALETTEBAR_POS_X+margin		: y = PALETTEBAR_POS_Y+margin
			foreach EditPaletteTable
				c = RefToR(EditPaletteTable(cnt)), RefToG(EditPaletteTable(cnt)), RefToB(EditPaletteTable(cnt))
				mgButtonColor bg, c, c, c
				mgButton "■", *__PaletteButton, x+(w+margin)*cnt, y, w, h, 1
				PaletteButtonID(cnt) = stat
			loop
			//全消しボタン設置
			x = (PALETTEBAR_POS_X+margin)+(PALETTEBAR_SIZE_H-margin)*length(EditPaletteTable)
			y = PALETTEBAR_POS_Y+margin
			w = PALETTEBAR_SIZE_H-margin*2	: h = w
			bgh = bg(0)+8, bg(1)+8, bg(2)+8
			mgButtonColor bg, bgh, bg, bg
			mgButton "", *__ClearButton, x, y, w, h, 1, SCR_BUTTONICON, 0
			//アンドゥボタン設置
			x += w+margin
			mgButtonColor bg, bgh, bg, bg
			mgButton "", *__AndoButton, x, y, w, h, 1, SCR_BUTTONICON, 2
			UndoButtonID = stat
		return

	//----------------------------------------------------------------------------------------------
	//= パレットボタン
	//----------------------------------------------------------------------------------------------
		*__PaletteButton
			foreach PaletteButtonID
				if( PaletteButtonID(cnt) == stat ){
					id = cnt
					break
				}
			loop
			EditPenColor = EditPaletteTable(id)
		return

	//----------------------------------------------------------------------------------------------
	//= 全消しボタン
	//----------------------------------------------------------------------------------------------
		*__ClearButton
			gsel SCR_PENBUF
			COLSET_ALPHA
			boxf
			gsel SCR_EDIT
		return

	//----------------------------------------------------------------------------------------------
	//= アンドゥボタン
	//----------------------------------------------------------------------------------------------
		*__AndoButton
			mgButtonPrm UndoButtonID, , , , , , , , , 2
			gsel SCR_PENBUF
			gmode 1, CaptureSizeW, CaptureSizeH
			pos 0, 0
			gcopy SCR_ANDOBUF
			gsel SCR_EDIT
		return

	//----------------------------------------------------------------------------------------------
	//= アンドゥバッファ生成
	//----------------------------------------------------------------------------------------------
		*__MakeAndoBuffer
			mgButtonPrm UndoButtonID, , , , , , , , , 1
			gsel SCR_ANDOBUF
			gmode 1, CaptureSizeW, CaptureSizeH
			pos 0, 0
			gcopy SCR_PENBUF
			gsel SCR_EDIT
		return

	//----------------------------------------------------------------------------------------------
	//= 保存画像バッファ作成
	//----------------------------------------------------------------------------------------------
		*__MakeSaveBuffer
			buffer SCR_SAVEBUF, CaptureSizeW, CaptureSizeH
			gmode 0
			pos 0, 0
			gcopy SCR_SSBUF, 0, 0, CaptureSizeW, CaptureSizeH
			COLSET_ALPHA
			gmode 4, , , 256
			pos 0, 0
			gcopy SCR_PENBUF, 0, 0, CaptureSizeW, CaptureSizeH
		return

	//----------------------------------------------------------------------------------------------
	//= サムネイルバッファ作成
	//----------------------------------------------------------------------------------------------
		*__MakeThumbnailBuffer
			//画像の中心を正方形で抜き出す
			if( CaptureSizeW <= CaptureSizeH ){
				size = CaptureSizeW
			} else {
				size = CaptureSizeH
			}
			buffer SCR_THUMBNAILBUF1, size, size
			x = size/2-CaptureSizeW/2
			y = size/2-CaptureSizeH/2
			gmode 0, CaptureSizeW, CaptureSizeH
			pos x, y
			gcopy SCR_SAVEBUF
			//サムネイルを作成
			buffer SCR_THUMBNAILBUF2, THUMBNAIL_SIZE_WH, THUMBNAIL_SIZE_WH
			pos 0, 0
			gzoom THUMBNAIL_SIZE_WH, THUMBNAIL_SIZE_WH, SCR_THUMBNAILBUF1, 0, 0, size, size, 1
		return

	//----------------------------------------------------------------------------------------------
	//= タイムスタンプファイル作成
	//----------------------------------------------------------------------------------------------
		*__MakeTimeStamp
			//日付を取得
			year	= strf("%04d", gettime(0))
			month	= strf("%02d", gettime(1))
			day		= strf("%02d", gettime(3))
			hour	= strf("%02d", gettime(4))
			minute	= strf("%02d", gettime(5))
			second	= strf("%02d", gettime(6))
			time = ""+year+"/"+month+"/"+day+" "+hour+":"+minute+":"+second
			//一時ファイル作成
			tmp = ""
			notesel tmp
			noteadd ""+time, 0, 1
			notesave TMP_TIMESTAMP
		return

	//----------------------------------------------------------------------------------------------
	//= アップロードボタン
	//----------------------------------------------------------------------------------------------
		*__Upload
			gosub *__MakeSaveBuffer
			pngsave TMP_IMAGE
			gosub *__MakeThumbnailBuffer
			pngsave TMP_THUMBNAIL
			gosub *__MakeTimeStamp
			gosub *__UploadStart
			if( stat == 0 ){
				delete TMP_IMAGE
				delete TMP_THUMBNAIL
				delete TMP_TIMESTAMP
				end
			}
		return

	//----------------------------------------------------------------------------------------------
	//= アップロード処理
	//----------------------------------------------------------------------------------------------
		*__UploadStart
			//FTPサーバーへ接続
			ftpopen ""+FtpHost, ""+USERNAME_HEADER+FtpUserName, ""+FtpPassword
			if( stat != 0 ){
				msg = {"データベースサーバーへのログインに失敗しました。
				ユーザー名またはパスワードが間違っている可能性があります。
				(ユーザー名/パスワードが正しいことを確認した上で
				長期的にこの問題が解決しない場合は管理者へ知らせてください。)"}
				dialog ""+msg, 1, "エラー"
			}
			//アップロードファイル名を生成
			repeat
				//8桁のランダム英数字を生成
				filename = ""+FileNameMake()
				//同名ファイルがリモートに既に存在するかチェック
				if( ftpexist(""+filename+".png") != 0 ){
					break
				} else {
					dialog "既に存在するよ："+filename
				}
			loop
			//アップロードパス生成
			pathImage = ""+filename+".png"
			pathThumb = ""+filename+".thumb.png"
			pathStamp = ""+filename+".stamp.txt"
			//アップロード
			f = 0
			ftpput ""+TMP_IMAGE, ""+pathImage, 0		:f += stat
			ftpput ""+TMP_THUMBNAIL, ""+pathThumb, 0	:f += stat
			ftpput ""+TMP_TIMESTAMP, ""+pathStamp, 0	:f += stat
			if( f == 0){
				//クリップボードにセット
				tmp = "http://"+FtpHost+"/hijkcapture/"+FtpUserName+"/"+pathImage
				clipset ""+tmp
				//ブラウザで開く
				tmp = "http://"+FtpHost+"/hijkcapture/open.php?file="+pathImage
				exec ""+tmp, 16
			} else {
				dialog "アップロードに失敗しました。", 1, "エラー"
			}
			//サーバーから切断する
			ftpclose
		return f

	//----------------------------------------------------------------------------------------------
	//= ローカルへ保存ボタン
	//----------------------------------------------------------------------------------------------
		*__LocalSave
			dialog "png",17,"PNG"
			if( stat == 1 ){
				savepath = ""+getpath(refstr, 1)+".png"
				gosub *__MakeSaveBuffer
				pngsave ""+savepath
				goto *__end
			}
		return

	//----------------------------------------------------------------------------------------------
	//= キャンセルボタン
	//----------------------------------------------------------------------------------------------
		*__Cancel
			goto *__end
		return

	//----------------------------------------------------------------------------------------------
	//= 範囲選択中の操作方法ウィンドウ内容描画
	//----------------------------------------------------------------------------------------------
		*__SelectHelpDraw
			color				:boxf
			color 255,255,196	:boxf 1, 1, SELECTHELP_SIZE_W-2, SELECTHELP_SIZE_H-2
			color				:font msgothic, 12	:pos 8, 8
			mes "Ctrl+ドラッグで範囲選択"
			mes "ESCキーでソフトウェアを終了"
		return

	//----------------------------------------------------------------------------------------------
	//= マウスが編集画面有効範囲内にあるかを判別
	//----------------------------------------------------------------------------------------------
		#defcfunc MouseEditRange
			ret = 0
			if( MousePosX() >= 0 ){
				if( MousePosX() <= EDITWINDOW_SIZE_W ){
					if( MousePosY() >= 0+PALETTEBAR_SIZE_H ){
						if( MousePosY() <= EDITWINDOW_SIZE_H-CTRLBAR_SIZE_H ){
							ret = 1
						}
					}
				}
			}
		return ret